set title "Random Access Scales Quadratically\nwith Features" offset -5,4 font ",40"
set xlabel "Number of features" font ",30"
set ylabel "Execution time (ms)" font ",30" offset -7,0

set xrange[1:100]
set yrange[0:120]
set border 3
set tics scale 0 font ",20"
set bmargin 5
set lmargin 16
set tmargin 7
set rmargin 5

set key spacing 5 font ",28" at graph 1, 1
#set key off

#set terminal pngcairo size 1070, 860
#set output 'performance_vs_features.png'

set termoption enhanced
f(x) = a*x**b + c;
fit f(x) "randomAccess.dat" using 1:2 via a,b,c
g(x) = d*exp(h*x);
d = 0.0305332371;
h = 0.6663482219;

plot "randomAccess.dat" using 1:2 linecolor rgb "blue" title "CtxList", \
"randomAccess.dat" using 1:3 linecolor rgb "red" title "V<List>", \
f(x) with lines linecolor rgb "blue" lw 4 title sprintf("CtxList trend: %.4f x^{%.2f} + %.2f", a, b, c), \
g(x) with lines linecolor rgb "red" lw 4 title sprintf("V<List> trend: %.2f e^{%.2f x}", d, h)

pause -1
