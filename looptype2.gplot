set title "Loop Type 2 Scales Cubically\nwith Features" offset -5,4 font ",40"
set xlabel "Number of features" font ",30"
set ylabel "Execution time (ms)" font ",30" offset -7,0

set xrange[1:100]
set yrange[0:1200]
set border 3
set tics scale 0 font ",20"
set bmargin 5
set lmargin 16
set tmargin 7
set rmargin 5

set key spacing 5 font ",28" at graph 0.8, 1
#set key off

#set terminal pngcairo size 1070, 860
#set output 'performance_vs_features.png'

set termoption enhanced
f(x) = a*x**b + c;
fit f(x) "looptype2.dat" using 1:2 via a,b,c
g(x) = d*exp(h*x);
fit [1:10] g(x) "looptype2.dat" using 1:3 via d,h

plot "looptype2.dat" using 1:2 linecolor rgb "blue" title "CtxList", \
"looptype2.dat" using 1:3 linecolor rgb "red" title "V<List>", \
f(x) with lines linecolor rgb "blue" lw 4 title sprintf("CtxList trend: %.4f x^{%.2f} + %.2f", a, b, c), \
g(x) with lines linecolor rgb "red" lw 4 title sprintf("V<List> trend: %.4f e^{%.2f x}", d, h)

pause -1
